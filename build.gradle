plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id "com.github.ben-manes.versions" version "0.21.0"
    //id "io.freefair.lombok" version "3.1.4"
    id 'net.ltgt.apt' version '0.21'
    id 'org.beryx.jlink' version '2.10.2'
}

repositories {
    mavenLocal()
    maven { url = 'http://jcenter.bintray.com' }
    maven { url = 'http://repo.maven.apache.org/maven2' }
    maven { url 'https://projectlombok.org/edge-releases' }
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.0.0'
    implementation 'com.jfoenix:jfoenix:9.0.8'
    implementation 'org.fxmisc.richtext:richtextfx:0.10.0'
    implementation 'com.1stleg:jnativehook:2.1.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.json:json:20180813'
    implementation group: 'eu.hansolo', name: 'Medusa', version: '8.3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    implementation 'com.google.code.gson:gson:2.8.5'
    
    //implementation 'com.github.almasb:fxgl:11.1-beta'
       
    implementation files('libs/scenicview.jar')
    
    compileOnly 'org.projectlombok:lombok:1.18.6'
    compileOnly group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.0.Final'
    
	annotationProcessor 'org.projectlombok:lombok:1.18.6'
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.0.Final'
}

javafx {
    modules = [ 'javafx.controls',
    			'javafx.fxml',
    			'javafx.graphics',
    			'javafx.swing',
    			'javafx.web',
    			'javafx.media' ]
}

eclipse {
    classpath {
        file {
            whenMerged {
                entries.findAll { 
                	it.kind == 'src' || it.kind == 'lib' 
                }.each { it.entryAttributes['module'] = 'true' }
            }
        }
    }
}

group = 'pl.hopelew'
version = '0.1'
description = 'jRPG NoName Game'
ext.moduleName = 'jrpg'
mainClassName = moduleName + '/pl.hopelew.jrpg.Main'

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Automatic-Module-Name': moduleName
    }
    inputs.property("moduleName", moduleName)
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'jrpg'
        jvmArgs = ['--add-exports javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix', '--add-exports javafx.controls/com.sun.javafx.scene.control=com.jfoenix', '--add-exports javafx.base/com.sun.javafx.binding=com.jfoenix', '--add-exports javafx.graphics/com.sun.javafx.stage=com.jfoenix', '--add-exports javafx.base/com.sun.javafx.event=com.jfoenix -Dprism.verbose=true']
        
    }
    addExtraDependencies("javafx")
    imageName = 'jrpg-SNAPSHOT'
    
}

/*
tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
} */

